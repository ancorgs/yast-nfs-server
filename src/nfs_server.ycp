/**
 * File:
 *   nfs_server.ycp
 *
 * Module:
 *   Configuration of nfs_server
 *
 * Summary:
 *   Module for the configuration of the nfs server
 *
 * Authors:
 *   Jan Holesovsky <kendy@suse.cz>
 *   Dan Vesely <dan@suse.cz>
 *
 * $Id$
 *
 * Module for the configuration of the nfs server
 */

/***
 * <h3>Configuration of the nfs_server</h3>
 */

{

    textdomain "nfs_server";

    import "NfsServer";
    include "require.ycp";
    import "Wizard";
    include "wizard/sequencer.ycp";
    include "nfs_server/ui.ycp";

    map Aliases = $[
	"begin"   : ``(BeginDialog ()),
	"exports"   : ``(ExportsDialog ()),
	"end"     : ``(SaveDialog ())
	];

    map Sequence = $[
	"ws_start"  : "begin",
	"begin"     : $[
	    `next   : "exports",
	    `finish : "end",
	    `abort  : `abort
	    ],
	"exports"       : $[
	    `next   : "end",
	    `abort  : `abort
	    ],
	"end"       : $[
	    `finish    : `ws_finish
	    ]
	];

    // If nfs-utils nor nfs-server is not installed, install nfs-utils.
    // TODO: require installation of alternate packages
    if (!SCR::Read (.targetpkg.installed, "nfs-server"))
    {
	use_star_for_anonymous = true;
	spaces_allowed = true;
	if (!RequireAndConflict (["nfs-utils"], [],
			     // notification 1/2
			     _("<p>To configure the Network File System, the <b>%1</b> package must be installed.</p>") +
			     // notification 2/2
			     _("<p>Do you want to install it now?</p>")))
	{
	    return;
	}
    }

    if (! NfsServer::Read ())
    {
	// TODO:log an error?
	return;
    }

    Wizard::CreateDialog ();

    any result = WizardSequencer (Aliases, Sequence);

    if (result == `ws_finish)
    {
	if (NfsServer::Write())
	{
	    UI::MessagePopup (_("All the settings were written and are ready to use."));
	}
    }

    return UI::CloseDialog();
}
