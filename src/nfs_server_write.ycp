/**
 * File:
 *   nfs_server_write.ycp
 *
 * Module:
 *   Configuration of nfs_server
 *
 * Summary:
 *   Writing only client
 *
 * Authors:
 *   Jan Holesovsky <kendy@suse.cz>
 *   Dan Vesely <dan@suse.cz>
 *
 * $Id$
 *
 * This is a write-only client. It takes its arguments and just
 * write the settings.
 */ 

{
  
    textdomain "lan";

    include "ui/common_popups.ycp";

    /**
     * Saves NFS server part
     * @param start_nfs_server start NFS server
     * @param export list of export entries
     * @return boolean success
     */
    global define nfs_server_save (boolean start_nfs_server, list exports) ``{

        SCR (`Execute(.target.bash, "/etc/init.d/nfsserver stop", $[]));
        
        if (start_nfs_server) 
        {
            if (exports != nil) 
            {
                SCR (`Execute (.target.bash, "/bin/cp $ORIG $BACKUP", $["ORIG" : "/etc/exports", "BACKUP" : "/etc/exports.YaST2.save"]));

                if (SCR(`Write(.etc.exports, exports)) == nil) 
                {
                    // error popup message
                    UI(`MessagePopup(_("I was unable to write to /etc/exports.
There will be no changes in the exported
directories.")));
                    return false;
                }
                
		SCR(`Write(.rc.system.START_PORTMAP, "yes"));
                SCR(`Write(.rc.system.NFS_SERVER, "yes"));
                
                SCR (`Execute(.target.bash, "/etc/init.d/portmap start", $[]));                // but this is necessary
                Shell ("/etc/init.d/nfsserver start");
                
                if (SCR (`Execute(.target.bash, "/etc/init.d/nfsserver status | /bin/grep up", $[])) != 0) 
                {
                    // error popup message
                    UI(`MessagePopup(_("I was unable to restart the NFS server.
Your changes will be active after reboot.")));
                    return false;
                }
            }
        } 
        else
        {
            SCR(`Write(.rc.system.NFS_SERVER, "no"));
        }
        
        if (! SCR(`Write(.rc.system, nil))) 
        {                  // Perform the write
            // error popup message
            UI(`MessagePopup(_("I was unable to write to /etc/rc.config.
After reboot the NFS server may have
unexpected behavior.")));
            return false;
        } 
        else
        {
            SCR (`Execute(.target.bash, "/sbin/SuSEconfig -quick --nonewpackage", $[]));
        }
        
        return true;
    }

  /* The end of the definitions */
  
}
